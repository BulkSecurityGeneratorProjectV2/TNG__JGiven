buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 27

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    dexOptions {
        additionalParameters '--debug'
    }
    afterEvaluate {
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.release
                    pom {
                        name = project.name
                        url = 'http://jgiven.org'
                        // to be able use project.description which may be overridden in later executed build.gradle of subproject
                        afterEvaluate {
                            description = project.description
                        }

                        scm {
                            url = 'scm:git@github.com:TNG/jgiven.git'
                            connection = 'scm:git@github.com:TNG/jgiven.git'
                            developerConnection = 'scm:git@github.com:TNG/jgiven.git'
                        }

                        licenses {
                            license {
                                name = 'The Apache Software License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution = 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'janschaefer'
                                name = 'Jan Sch√§fer'
                            }
                            developer {
                                id = 'l-1squared'
                                name = 'Kristof Karhan'
                            }
                        }

                    }
                }
            }
            repositories {
                maven {
                    String releaseUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    String snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                    url = System.env.RELEASE == "true" ? releaseUrl : snapshotUrl
                    credentials {
                        username = sonatypeUsername
                        password = sonatypePassword
                    }
                }
            }
        }
    }

}

dependencies {
    api "com.google.guava:guava:$guavaAndroidVersion"
    api("net.bytebuddy:byte-buddy-android:$byteBuddyVersion")
    api "junit:junit:$junitVersion"
    implementation "com.android.support:appcompat-v7:$androidBuildToolsVersion"
    implementation('com.android.support.test:rules:0.5') {
        exclude group: "junit"
    }
    //implementation files('libs/dx-1.7.jar')
    implementation(project(path: ':jgiven-junit')) {
        exclude group: "com.google.guava"
        exclude group: "net.bytebuddy"
    }
    implementation(project(path: ':jgiven-core')) {
        exclude group: "com.google.guava"
        exclude group: "net.bytebuddy"
    }
}

